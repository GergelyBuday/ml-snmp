%%
%name	ObjectType
%term ACCESS | 
BEGIN |
COMMA |
COUNTER |
CSTRING of string | 
DEFINITIONS |
DEPRECATED_ | 
DESCRIPTION | 
DISPLAYSTRING |
DOUBLEDOT |
END |
EOF |
GAUGE |
ID of string |
IDENTIFIER |
INDEX |
INTEGER | 
IPADDRESS |
LBRACE | 
LETITBE | 
LPAREN |
MANDATORY_ | 
NETWORKADDRESS |
NOTACCESSIBLE_ |
NULL | 
NUMBER of int | 
OBJECT |
OBJECT_TYPE |
OBSOLETE_  |  
OF |
OPTIONAL_  | 
PHYSADDRESS |
RBRACE | 
READONLY_  | 
READWRITE_ | 
REFERENCE of string | 
RPAREN |
SEQUENCE |
SIZE |
STATUS | 
SYNTAX | 
TIMETICKS |
WRITEONLY_ 

%nonterm  ACCESSTYPE of Mib.accesstype
	| ASSIGNMENT of Mib.assignment
	| ASSIGNMENTLIST of Mib.assignment list
	| BASICTYPE of Mib.basictype
	| DESCRPART of string option  
	| DISPLAYSTRINGTYPE of Mib.basictype
	| ELEMENTTYPE of string * Mib.basictype
	| ELEMENTTYPELIST of (string * Mib.basictype) list
	| IMPORTSPART
	| INDEXPART of Mib.mibtype list 
	| INDEXTYPE of Mib.mibtype
	| INDEXTYPES of Mib.mibtype list
	| INTEGER_TYPE of Mib.basictype
	| MIBTYPE of Mib.mibtype
	| MIB_VARIABLE_ASSIGNMENT of Mib.rawobject
	| MODULEDEFINITION of Mib.assignment list
	| MODULEBODY of Mib.assignment list
	| NAMEDNUMBER of string * int
	| NAMEDNUMBERLIST of (string * int) list
	| OBJ_ID_VALUE of (string * int)
	| SEQUENCE_TYPE of (string * Mib.basictype) list
	| SEQUENCE_OF_TYPE of Mib.mibtype
	| STATUSTYPE of Mib.statustype
	| TYPEASSIGNMENT of string * Mib.mibtype
	| VALUEASSIGNMENT of string * string * int

%pos int

%verbose
%noshift EOF
%eop EOF
%%


MODULEDEFINITION:REFERENCE DEFINITIONS LETITBE 
		 BEGIN MODULEBODY END	(MODULEBODY)

MODULEBODY	:ASSIGNMENTLIST		(ASSIGNMENTLIST)

ASSIGNMENTLIST	:ASSIGNMENT ASSIGNMENTLIST	(ASSIGNMENT::ASSIGNMENTLIST)
		|ASSIGNMENT			([ASSIGNMENT])

ASSIGNMENT	:TYPEASSIGNMENT		 (Mib.TYPE TYPEASSIGNMENT)
		|VALUEASSIGNMENT	 (Mib.VALUE VALUEASSIGNMENT)
		|MIB_VARIABLE_ASSIGNMENT (Mib.OBJECT MIB_VARIABLE_ASSIGNMENT)

TYPEASSIGNMENT	:REFERENCE LETITBE MIBTYPE
					(REFERENCE,MIBTYPE)

VALUEASSIGNMENT	:ID OBJECT IDENTIFIER LETITBE OBJ_ID_VALUE
					(ID,#1 OBJ_ID_VALUE,#2 OBJ_ID_VALUE)

MIBTYPE		:BASICTYPE		(Mib.BASIC BASICTYPE)
		|SEQUENCE_TYPE		(Mib.SEQUENCE_T SEQUENCE_TYPE)
		|SEQUENCE_OF_TYPE	(Mib.SEQUENCEOF_T SEQUENCE_OF_TYPE)
		|REFERENCE		(Mib.REF REFERENCE)

BASICTYPE	:OBJECT IDENTIFIER (Mib.OBJ_ID_T)
		|INTEGER_TYPE   (INTEGER_TYPE)
		|NULL		(Mib.NULL_T)
		|NETWORKADDRESS	(Mib.NETWORK_ADDRESS_T)
		|IPADDRESS	(Mib.IP_ADDRESS_T)
		|COUNTER	(Mib.COUNTER_T)
		|GAUGE		(Mib.GAUGE_T)
		|TIMETICKS	(Mib.TIMETICKS_T)
		|DISPLAYSTRINGTYPE  (DISPLAYSTRINGTYPE)
		|PHYSADDRESS	(Mib.PHYS_ADDRESS_T)

DISPLAYSTRINGTYPE:DISPLAYSTRING 
		 LPAREN SIZE 
		 LPAREN NUMBER DOUBLEDOT NUMBER RPAREN 
		 RPAREN		(Mib.DISPLAYSTRING_T (SOME (NUMBER1,NUMBER2)))
		|DISPLAYSTRING	(Mib.DISPLAYSTRING_T NONE)

INTEGER_TYPE :INTEGER LBRACE NAMEDNUMBERLIST RBRACE
			(Mib.INT_T (Mib.NAME_AND_NUMBER_LIST NAMEDNUMBERLIST))
   |INTEGER		(Mib.INT_T Mib.SIMPLE)
   |INTEGER LPAREN NUMBER DOUBLEDOT NUMBER RPAREN
			(Mib.INT_T (Mib.SUBTYPE_SPEC (NUMBER1,NUMBER2)))
		
NAMEDNUMBERLIST	:NAMEDNUMBER 		([NAMEDNUMBER])
		|NAMEDNUMBER COMMA NAMEDNUMBERLIST
					(NAMEDNUMBER::NAMEDNUMBERLIST)

NAMEDNUMBER	:ID LPAREN NUMBER RPAREN		((ID,NUMBER))

SEQUENCE_TYPE	:SEQUENCE LBRACE ELEMENTTYPELIST RBRACE	(ELEMENTTYPELIST)

ELEMENTTYPELIST	:ELEMENTTYPE COMMA ELEMENTTYPELIST
					(ELEMENTTYPE::ELEMENTTYPELIST)
		|ELEMENTTYPE		([ELEMENTTYPE])

ELEMENTTYPE	:ID BASICTYPE		(ID,BASICTYPE) 

SEQUENCE_OF_TYPE: SEQUENCE OF MIBTYPE	(MIBTYPE)

MIB_VARIABLE_ASSIGNMENT: 
		ID OBJECT_TYPE 
		SYNTAX MIBTYPE
		ACCESS ACCESSTYPE
		STATUS STATUSTYPE
		DESCRPART INDEXPART
		LETITBE OBJ_ID_VALUE 
		({name = ID,syntax = MIBTYPE,access = ACCESSTYPE,
		  status = STATUSTYPE,descr = DESCRPART,
		  index = INDEXPART,objid = OBJ_ID_VALUE}:Mib.rawobject) 

ACCESSTYPE	: READONLY_		(Mib.READONLY)
		| READWRITE_ 		(Mib.READWRITE)
		| WRITEONLY_		(Mib.WRITEONLY) 
		| NOTACCESSIBLE_	(Mib.NOTACCESSIBLE)

STATUSTYPE	: MANDATORY_		(Mib.MANDATORY)
		| OPTIONAL_		(Mib.OPTIONAL)
		| OBSOLETE_		(Mib.OBSOLETE)
		| DEPRECATED_		(Mib.DEPRECATED)

DESCRPART	: DESCRIPTION CSTRING 	(SOME CSTRING)
		| 			(NONE)

INDEXPART	: INDEX LBRACE INDEXTYPES RBRACE	(INDEXTYPES) 
		| 					([])

INDEXTYPES	:INDEXTYPE			([INDEXTYPE])
		|INDEXTYPE COMMA INDEXTYPES	(INDEXTYPE::INDEXTYPES)

INDEXTYPE	:ID				(Mib.REF ID)

OBJ_ID_VALUE	:LBRACE ID NUMBER RBRACE    (ID,NUMBER)
